varnishtest "Test fsbackend vmod"

server s1 {
       rxreq
       txresp
} -start

# Create root directory
shell "mkdir ${tmpdir}/fsb"
shell "ln -s ${tmpdir}/fsb/foo ${tmpdir}/fsb/foolink"

varnish v1 -vcl+backend {
	import fsbackend from "${vmod_topbuild}/src/.libs/libvmod_fsbackend.so";

	sub vcl_init {
		new root = fsbackend.root("${tmpdir}/fsb");
	}

	sub vcl_hit {
		set req.http.hit = true;
	}

	sub vcl_backend_fetch {
		set bereq.backend = root.backend();
	}

	sub vcl_deliver {
		set resp.http.hit = req.http.hit;
	}
} -start

# Test normal file delivery
shell "echo -n foo > ${tmpdir}/fsb/foo"
client c1 {
	txreq -url /foo
	rxresp
	expect resp.status == 200
	expect resp.http.last-modified != "<undef>"
	expect resp.body == "foo"
} -run

# Check that the result enters the cache
client c1 {
	txreq -url /foo
	rxresp
	expect resp.status == 200
	expect resp.body == "foo"
	expect resp.http.hit == "true"
} -run

# Test /foo written in percent-encoding
client c1 {
	txreq -url /%66%6f%6F
	rxresp
	expect resp.status == 200
	expect resp.body == "foo"
} -run

# Test file named '%'
shell "echo -n percent > ${tmpdir}/fsb/%"
client c1 {
	txreq -url /%%
	rxresp
	expect resp.status == 200
	expect resp.body == "percent"
} -run

# Test file named '&'
shell "echo -n ampersand > ${tmpdir}/fsb/\\&"
client c1 {
	txreq -url /%26
	rxresp
	expect resp.status == 200
	expect resp.body == "ampersand"
} -run

# Check that directory listing gives 403
client c1 {
	txreq -url /
	rxresp
	expect resp.status == 403
} -run

# Check that symlink out of root directory gives 403
shell "echo -n bar > ${tmpdir}/bar"
shell "ln -s ../bar ${tmpdir}/fsb/barlink"
client c1 {
	txreq -url /barlink
	rxresp
	expect resp.status == 403
} -run

# Check that symlink inside of root is ok
shell "mkdir ${tmpdir}/fsb/dir"
shell "ln -s ../foo ${tmpdir}/fsb/dir/foolink"
client c1 {
	txreq -url /foolink
	rxresp
	expect resp.status == 200
	expect resp.body == "foo"
} -run

# Check that ../ inside of root is ok
client c1 {
	txreq -url /dir/../foo
	rxresp
	expect resp.status == 200
	expect resp.body == "foo"
} -run

# Check that ../ outside of root gives 403
client c1 {
	txreq -url /../bar
	rxresp
	expect resp.status == 403
} -run
